plugins {
	id 'org.springframework.boot' version '3.3.7' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
}

ext {
	springCloudVersion = '2023.0.4'
}

allprojects {
	group = 'com.ai.food.tracker'
	version = '0.0.1-SNAPSHOT'

	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
			mavenBom "org.springframework.ai:spring-ai-bom:1.0.0"
		}
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
	tasks.register('startAllServices') {
		group = 'application'
		description = 'Run all microservices (auth, gateway, food, calorie) in parallel'
		dependsOn(
				':auth-service:bootRun',
				':api-gateway:bootRun',
				':food-service:bootRun',
				':calorie-service:bootRun'
		)
	}
}